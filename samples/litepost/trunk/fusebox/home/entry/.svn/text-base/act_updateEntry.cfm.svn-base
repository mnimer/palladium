<!---------------------------

act_updateEntry

Updates the specified entry

----------------------------->

<!---- set default parameters ---->
<cftry>
	<cfparam name="ATTRIBUTES.entryID" default="0" type="string" />
	<cfparam name="ATTRIBUTES.categoryID" default="0" type="string" />
	<cfparam name="ATTRIBUTES.title" default="" type="string" />
	<cfparam name="ATTRIBUTES.body" default="" type="string" />
	<cfcatch type="any">
		<cfthrow type="validation" message="Missing/Invalid Field" detail="One of the required fields is missing or invalid.">
	</cfcatch>
</cftry>

<!---- validate passed arguments ---->
<cfif NOT isValidID(ATTRIBUTES.entryID) OR ATTRIBUTES.entryID EQ 0>
	<cfthrow type="validation.INVALID_ID" message="Invalid Entry" detail="You must specify a valid entry." />
</cfif>
<cfif NOT isValidID(ATTRIBUTES.categoryID)>
	<cfthrow type="validation.INVALID_ID" message="Invalid Category" detail="You must specify a valid category." />
</cfif>
<cfif isEmpty(ATTRIBUTES.title)>
	<cfthrow type="validation.REQUIRED_FIELD" message="Entry Title Required" detail="You must specify a title for the entry." />
</cfif>
<cfif isEmpty(ATTRIBUTES.body)>
	<cfthrow type="validation.REQUIRED_FIELD" message="Entry Body Required" detail="You must specify the body of the entry." />
</cfif>

<!---- update entry ---->
<cfquery datasource="#APPLICATION.settings.dsn.name#" username="#APPLICATION.settings.dsn.username#" password="#APPLICATION.settings.dsn.password#">
	UPDATE entries
	SET categoryID = <cfqueryparam cfsqltype="cf_sql_integer" value="#ATTRIBUTES.categoryID#" />,
		title = <cfqueryparam cfsqltype="cf_sql_varchar" maxlength="255" value="#ATTRIBUTES.title#" />,
		body = <cfqueryparam cfsqltype="cf_sql_longvarchar" value="#ATTRIBUTES.body#" />,
		dateLastUpdated = <cfqueryparam cfsqltype="cf_sql_timestamp" value="#Now()#" />
	WHERE entryID = <cfqueryparam cfsqltype="cf_sql_integer" value="#ATTRIBUTES.entryID#" />
</cfquery>